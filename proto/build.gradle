plugins {
    id "net.ltgt.apt" version "0.20"
    id "com.diffplug.gradle.spotless" version "3.24.1"
    id "com.google.protobuf"  version "0.8.10"
    id "java"
    id "idea"
    id "maven-publish"
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile("com.google.protobuf:protobuf-java:${versions.protobuf}")
    compile("io.grpc:grpc-stub:${versions.grpc}")
    compile("io.grpc:grpc-protobuf:${versions.grpc}")
    runtime("io.grpc:grpc-netty-shaded:${versions.grpc}")
    compile("io.grpc:grpc-services:${versions.grpc}")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${versions.protobuf}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${versions.grpc}"
        }
    }
    
    generateProtoTasks {
        ofSourceSet('main').each {
            task ->
                task.plugins {
                    grpc {
                    }
                }
                task.generateDescriptorSet = true
                task.descriptorSetOptions.includeSourceInfo = true
                task.descriptorSetOptions.includeImports = true
        }
        ofSourceSet("test")

    }
}

spotless {
    java {
        googleJavaFormat('1.7')
        target project.fileTree(project.projectDir) {
            include "**/*.java"
            exclude "build/**/*.java"
        }
    }
}

publishing {
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            if (project.version.endsWith('-SNAPSHOT')) {
                url = uri(snapshotRepoUrl)
            } else {
                url = uri(repoUrl)
            }
            if (project.hasProperty('repoUser') && project.hasProperty('repoPass')) {
                credentials {
                    username repoUser
                    password repoPass
                }
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId 'io.anemos'
            artifactId 'metastore-proto'
            from components.java
        }
    }
}

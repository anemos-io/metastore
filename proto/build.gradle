plugins {
    id "net.ltgt.apt" version "0.20"
    id "com.diffplug.gradle.spotless" version "3.23.0"
    id "com.google.protobuf"  version "0.8.8"
    id "java"
    id "idea"
    id "maven-publish"
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile("com.google.protobuf:protobuf-java:3.6.1")
    compile("io.grpc:grpc-stub:1.17.1")
    compile("io.grpc:grpc-protobuf:1.17.1")
    runtime("io.grpc:grpc-netty-shaded:1.17.1")
    compile("io.grpc:grpc-services:1.17.1")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.6.1"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.15.1"
        }
    }
    
    generateProtoTasks {
        ofSourceSet('main').each {
            task ->
                task.plugins {
                    grpc {
                    }
                }

        }
        ofSourceSet("test")
    }
}

spotless {
    java {
        googleJavaFormat('1.7')
        target project.fileTree(project.projectDir) {
            include "**/*.java"
            exclude "build/**/*.java"
        }
    }
}

publishing {
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            if (project.version.endsWith('-SNAPSHOT')) {
                url = uri(snapshotRepoUrl)
            } else {
                url = uri(repoUrl)
            }
            if (project.hasProperty('repoUser') && project.hasProperty('repoPass')) {
                credentials {
                    username repoUser
                    password repoPass
                }
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId 'io.anemos'
            artifactId 'metastore-proto'
            from components.java
        }
    }
}

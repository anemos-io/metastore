import java.text.DateFormat
import java.text.SimpleDateFormat

plugins {
    id "com.diffplug.gradle.spotless" version "3.26.0"
    id "java"
    id "com.google.cloud.tools.jib" version '1.8.0'
    id "maven-publish"
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile(project(':proto'))
    compile(project(':core'))
    compile 'net.sourceforge.argparse4j:argparse4j:0.8.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

spotless {
    java {
        googleJavaFormat('1.7')
        target project.fileTree(project.projectDir) {
            include "**/*.java"
            exclude "build/**/*.java"
        }
    }
}

jib {
    from {
        image = containerRepoBase + '/distroless/java-protoc'
    }
    to {
        image = containerRepoTarget + '/metastep'
    }
    container {
        mainClass = 'io.anemos.metastore.metastep.MetaStep'
    }
}

processResources {
    doFirst {
        File file = new File("$sourceSets.main.output.resourcesDir/io/anemos/metastore/metastep/version.properties");
        file.getParentFile().mkdirs();
        file.withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            DateFormat dateFormat = new SimpleDateFormat("yyyy-MM-dd HH:mm:ss");
            Date date = new Date();
            p['build'] = dateFormat.format(date)
            p.store w, null
        }
    }
}

publishing {
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            if (project.version.endsWith('-SNAPSHOT')) {
                url = uri(snapshotRepoUrl)
            } else {
                url = uri(repoUrl)
            }
            if (project.hasProperty('repoUser') && project.hasProperty('repoPass')) {
                credentials {
                    username repoUser
                    password repoPass
                }
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId 'io.anemos'
            artifactId 'metastore-metastep'
            from components.java
        }
    }
}
